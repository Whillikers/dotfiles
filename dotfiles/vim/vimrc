""" This vimrc is a small 'core' of modifications relevant to both vim and
""" neovim. For full editing tasks, use nvim, configured in ~/.config/nvim.

" Disable vi compatibility
set nocompatible

""" Begin with tpope/vim-sensible equivalents (already run in neovim config)
if !has('nvim')
    set autoindent
    set backspace=indent,eol,start
    set complete-=i
    set smarttab

    set nrformats-=octal

    if !has('nvim') && &ttimeoutlen == -1
      set ttimeout
      set ttimeoutlen=100
    endif

    set incsearch
    " Use <C-L> to clear the highlighting of :set hlsearch.
    if maparg('<C-L>', 'n') ==# ''
      nnoremap <silent> <C-L> :nohlsearch<C-R>=has('diff')?'<Bar>diffupdate':''<CR><CR><C-L>
    endif

    set laststatus=2
    set ruler
    set wildmenu

    if !&scrolloff
      set scrolloff=1
    endif
    if !&sidescrolloff
      set sidescrolloff=5
    endif
    set display+=lastline

    if &encoding ==# 'latin1' && has('gui_running')
      set encoding=utf-8
    endif

    if &listchars ==# 'eol:$'
      set listchars=tab:>\ ,trail:-,extends:>,precedes:<,nbsp:+
    endif

    if v:version > 703 || v:version == 703 && has("patch541")
      set formatoptions+=j " Delete comment character when joining commented lines
    endif

    if has('path_extra')
      setglobal tags-=./tags tags-=./tags; tags^=./tags;
    endif

    if &shell =~# 'fish$' && (v:version < 704 || v:version == 704 && !has('patch276'))
      set shell=/usr/bin/env\ bash
    endif

    set autoread

    if &history < 1000
      set history=1000
    endif
    if &tabpagemax < 50
      set tabpagemax=50
    endif
    if !empty(&viminfo)
      set viminfo^=!
    endif
    set sessionoptions-=options
    set viewoptions-=options

    " Allow color schemes to do bright colors without forcing bold.
    if &t_Co == 8 && $TERM !~# '^Eterm'
      set t_Co=16
    endif
endif

""" Compatibility with XDG Base Directory scheme
" See: https://wiki.archlinux.org/index.php/XDG_Base_Directory
if !has('nvim')
    set undodir=$XDG_DATA_HOME/vim/undo
    set directory=$XDG_DATA_HOME/vim/swap
    set backupdir=$XDG_DATA_HOME/vim/backup
    set viewdir=$XDG_DATA_HOME/vim/view
    set viminfo+='1000,n$XDG_DATA_HOME/vim/viminfo
endif

""" General
set modelines=0 " Disable modelines for security reasons
set mouse=a " Enable mouse support
set hidden " Enable background buffers
set lazyredraw " Do not redraw the screen while executing macros
set undofile " Persist undo history across multiple sessions
set undodir=~/.vim/undo " Set undo directory
set ffs=unix,dos,mac " Default to Unix line endings
set backupcopy=yes " Modify files in place
set updatetime=300 " Write swap files to disk every 300ms

" Fix rendering with Kitty (see: https://github.com/kovidgoyal/kitty/issues/108)
let &t_ut=''

" Use system clipboard
set clipboard=unnamed
set clipboard+=unnamedplus

" Add multi-line movement to jump list
nnoremap <expr> k (v:count > 1 ? "m'" . v:count : '') . 'k'
nnoremap <expr> j (v:count > 1 ? "m'" . v:count : '') . 'j'

" Set keypress timeout
set ttimeoutlen=0 " NOTE: if this causes issues, set to 5; reduces time to esc
set timeoutlen=1000

" Avoid redrawing on every entered character by turning off arabic shaping
if has('arabic')
    set noarabicshape
endif

" Disable all conceals on the cursor line
set concealcursor=

""" Mappings
" Use <Space> as the leader
let mapleader = "\<Space>"

" , repeats last movement in normal and visual mode
nnoremap , ;
vnoremap , ;

" < reverses last movement in normal mode
nnoremap < ,

" ; jumps between matching braces
nnoremap ; %

" gp selects the last paste
nnoremap gp `[v`]

" ' jumps to a mark
nnoremap ' `

" ` activates the macro @a
nnoremap ` @a

" Enable repetition and macros across a visual selection
vnoremap . :normal .<CR>
vnoremap ` :normal @a<CR>

" Make Y work like C and D
nmap Y y$

" In vim, :W sudo saves the file
" (useful for handling the permission-denied error)
if !has('nvim')
    command! W execute 'w !sudo tee % > /dev/null' <bar> edit!
endif

" Move between splits with <Ctrl>-HJKL
nmap <C-j> <C-W>j
nmap <C-k> <C-W>k
nmap <C-h> <C-W>h
nmap <C-l> <C-W>l

" Return to last edit position when opening files
" From: https://github.com/amix/vimrc/blob/master/vimrcs/basic.vim
au BufReadPost * if line("'\"") > 1 && line("'\"") <= line("$") | exe "normal! g'\"" | endif

" Disable F1 (help) mapping
nmap <F1> <nop>

""" Search
set ignorecase smartcase " Only capital searches are case-sensitive
set gdefault " Replaces are global by default
set hlsearch " Highlight all search hits

" Persistent 'very magic' searches
nmap / /\v
nmap ? ?\v

""" Tabs
set tabstop=4 " Visual spaces per tab character
set softtabstop=4 " Spaces per soft-tab
set shiftwidth=4 " Amount to shift with < and >
set expandtab " Use <TAB> to insert spaces

""" Aesthetics
set termguicolors " Enable GUI colors in terminal mode
set showcmd  " Show partial commands in the bottom bar
syntax on " Enable syntax highlighting
set t_Co=256 " Enable 256 colors
set showmode " Show current mode (NOTE: overwritten in nvim config)
set number " Show current line number
set relativenumber " Use relative line numbers for all other numbers
set cursorline " Highlight the current line
set colorcolumn=80  " Show a column on line 80
set foldcolumn=1  " 1-character fold column

set noerrorbells " Disable audio bell
set novisualbell " Disable visual bell
